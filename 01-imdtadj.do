frames reset

*   2019 IMD        
*-----------------------

frame create imd2019
frame change imd2019


/// Load 2019 IMD dataset
import delimited "WxB-depvriation\Data\Source\IMD\IMDscores_main+dom_2019.csv", clear 

/// Keep area identifiers + IMD + health/crime scores
keep lsoacode2011 lsoaname2011 localauthoritydistrictcode2019 localauthoritydistrictname2019 indexofmultipledeprivationimdsco crimescore healthdeprivationanddisabilitysc incomescorerate employmentscorerate

/// Rename variables
rename lsoacode2011 lsoacode
rename lsoaname2011 lsoaname
rename localauthoritydistrictcode2019 lacode
rename localauthoritydistrictname2019 laname
rename indexofmultipledeprivationimdsco IMD
rename healthdeprivationanddisabilitysc health
rename crimescore crime
rename incomescorerate income
rename employmentscorerate employment

local dv IMD
local ivs ""


/// Back-transform health/crime tscores to ranks(1=least deprived)
*| And then from ranks to standardized scores (0/1 range)
foreach v of varlist health crime {
egen `v'rk = rank(`v'), track
gen `v'z = `v'rk/_N
local ivs "`ivs' `v'z"
}
*| Reversal of exponential transformation

//gen income_rate = income*100
//gen employment_rate = employment*100
//local ivs income_rate employment_rate

/// OLS regession of IMD score on health/crime ranks
reg `dv' `ivs'

di "R-squared for OLS linear model = `e(r2)'"
di "t-stat on health coef. = " e(b)[1,1]/sqrt(e(V)[1,1])
di "t-stat on crime coef. = " e(b)[1,2]/sqrt(e(V)[2,2])

/// Multivariable fractional polynomial (MFP) model with 2 degrees of freedom
mfp, df(2) center(no): reg `dv' `ivs'
gen healthzFP1 = healthz^3
gen crimezFP1 = crimez^2

eststo m1: reg IMD healthzFP1 crimezFP1
estadd beta: m1
eststo m1
esttab m1, beta


di "R-squared for MFP model (2df) = `e(r2)'"
di "t-stat on healthFP1 coef. = " e(b)[1,1]/sqrt(e(V)[1,1])
di "t-stat on crimeFP1 coef. = " e(b)[1,2]/sqrt(e(V)[2,2])
//qui est store mfp1

/// New frame for MFP 4df model
*| Cannot use same frame since variables generated by mfp program take same name
*| as as previous mfp model
//qui frame copy imd2019 imd2019fp2
//qui frame change imd2019fp2

/// Multivariable fractional polynomial (MFP) model with 4 degrees of freedom//
//mfp, df(4) center(no): reg `dv' `ivs'


//di "R-squared for MFP model (4df)  = `e(r2)'"
//di "t-stat on healthFP1 coef. = " e(b)[1,1]/sqrt(e(V)[1,1])
//di "t-stat on crimeFP1 coef. = " e(b)[1,2]/sqrt(e(V)[2,2])
//di "t-stat on healthFP2 coef. = " e(b)[1,3]/sqrt(e(V)[3,3])
//di "t-stat on crimeFP2 coef. = " e(b)[1,4]/sqrt(e(V)[4,4])

//di "MFP with 2df is substantial improvement on linear OLS in terms of variance-explained and precision"
//di "Increase in R-squared and precision for MFP with 4df versus 2df is negligible"

//qui frame change imd2019
//qui frame drop imd2019fp2

/// Residuals
//est restore mfp1
qui predict res2019, resid

/// Scalars for coefficient estimates
forvalues i = 1/3{
	
	scalar b`i'2019 = e(b)[1,`i']
}
scalar b02019 = b32019

/// Scalar for sd of residuals
qui sum res2019
scalar resSD2019 = r(sd)
di r(sd)

gen resz = res2019/resSD2019


/// Histogram
qui hist IMD, name(imd2019, replace)

//qui rename Iheal__1 healthzFP1
//qui rename Icrim__1 crimezFP1

/// Save dataset
qui gen year = 2019
qui gen IMDtadj = IMD
replace lsoacode = trim(lsoacode)
qui save "WxB-depvriation\Data\Derived\IMD\IMDtadj_2019", replace



*  2015 IMD time-adjusted
*--------------------------


frame create imd2015
frame change imd2015


/// Load England IMD dataset
qui import excel "WxB-depvriation\Data\Source\IMD\IMDscores_main+dom_2015.xlsx", sheet("ID2015 Scores") clear


/// Keep area identifiers + IMD score + health/crime scores
qui keep A B E F G I J
qui rename A lsoacode
qui rename B lsoaname
qui rename E IMD
rename F income
rename G employment
qui rename I health
qui rename J crime
qui drop if _n < 2


/// Destring
qui foreach v of varlist IMD income employment health crime {
	
    destring `v', gen(`v'2)
	drop `v'
	rename `v'2 `v'
}


local dv IMD
local ivs ""





//Convert health/crime scores to ranks (1=least deprived)
qui foreach v of varlist health crime {
	egen `v'rk = rank(`v'), track
	gen `v'z = `v'rk/_N
}

/// Degree-1 fractional polynomials
qui gen healthzFP1 = healthz^3
qui gen crimezFP1 = crimez^2

/// OLS regression of IMD score on health + crime domains
reg `dv' healthzFP1 crimezFP1

/// Standardized residuals
qui predict res2015, resid

/// Scalar for SD of residuals
qui su res2015
scalar resSD2015 = r(sd)

/// Adjusted IMD score (2015 score adjusted to 2019 scale)

qui gen IMDtadj = b02019 + (b12019*healthzFP1) + (b22019*crimezFP1) + ((res2015*resSD2019)/resSD2015)






/// Histogram
qui hist IMDtadj, name(IMDtadj2015, replace)

/// Save dataset
qui gen year = 2015
replace lsoacode = trim(lsoacode)
qui save "WxB-depvriation\Data\Derived\IMD\IMDtadj_2015", replace


* 2010 IMD time-adjusted
*--------------------------

qui frame create imd2010
qui frame change imd2010

qui import excel "WxB-depvriation\Data\Source\IMD\IMDscores_main_2010.xls", sheet("IMD 2010") clear

qui keep A F
qui rename A lsoacode
qui rename F IMD
qui drop if _n < 2
destring IMD, gen(IMDi)
drop IMD
rename IMDi IMD

/// New frame for health domain 
qui frame create imd2010health
qui frame change imd2010health

qui import excel "WxB-depvriation\Data\Source\IMD\IMDscores_health_2010.xls", sheet("Health & Disability") clear

qui keep A G
qui rename A lsoacode
qui rename G healthrk
qui drop if _n < 2

qui frame change imd2010
qui frlink 1:1 lsoacode, frame(imd2010health )

/// New frame for crime domain 
qui frame create imd2010crime
qui frame change imd2010crime

qui import excel "C:\Users\matti\Documents\WxB-depvriation\Data\Source\IMD\IMDscores_crime_2010.xls", sheet("Crime") clear

qui keep A G
qui rename A lsoacode
qui rename G crimerk
qui drop if _n < 2

qui frame change imd2010
qui frlink 1:1 lsoacode, frame(imd2010crime)

/// Get health and crime ranks

qui frget healthrk, from(imd2010health)
qui frget crimerk, from(imd2010crime)




/// Destring and back transform ranks to standard distribution (0/1 range)
local doms health crime

local ivs " "
qui foreach v in `doms' {
	
	drop imd2010`v'
	
	destring `v'rk, gen(`v')
	
	egen `v'rkrev = rank(`v'), field
	
	gen `v'z = `v'rkrev/ _N
	
	local ivs "`ivs' `v'z"
}

local dv IMD
/// OLS regession of IMD score on health/crime ranks
reg `dv' `ivs'

qui gen healthzFP1 = healthz^3
qui gen crimezFP1 = crimez^2

reg `dv' healthzFP1 crimezFP1

*| Signiciant improvement in R-squared and t-precision

/// Residuals
qui predict res2010, resid

/// Scalar for SD of residuals
qui su res2010
scalar resSD2010 = r(sd)

/// Adjusted IMD score (on English scale)
qui gen IMDtadj = b02019 + (b12019*healthzFP1) + (b22019*crimezFP1) + ((res2010*resSD2019)/resSD2010)

/// Histogram
qui hist IMDtadj, name(IMDtadj2010, replace)

/// Save dataset
qui gen year = 2010
replace lsoacode = trim(lsoacode)
qui save "WxB-depvriation\Data\Derived\IMD\IMDtadj_2010", replace


* 2010-2019 t-adjusted IMD score
*---------------------------------
frame create combine
frame change combine
use "WxB-depvriation\Data\Derived\IMD\IMDtadj_2019", clear
append using "WxB-depvriation\Data\Derived\IMD\IMDtadj_2015", keep(lsoacode IMD IMDtadj year)
order IMDtadj, after(IMD)
keep lsoacode lsoaname lacode laname IMD IMDtadj year

egen lsoaid = group(lsoacode)

/// Full time series from 2015-2019
tsset lsoaid year
tsfill, full

/// Update identifier variables
foreach v of varlist lsoacode lsoaname lacode laname{
	
	forvalues i = 1/4 {
		
		bysort lsoaid: replace `v' = `v'[_n+`i'] if `v' == ""
	}
}

/// Linear interpolation of IMD score
foreach v of varlist IMD IMDtadj {
    
	bysort lsoaid: ipolate `v' year, gen(`v'_ipol)
}

/// Save
order lsoaid
save "WxB-depvriation\Data\Derived\IMD\IMDtadj_2015-19", replace



* Save key files

* carry forward/iploate

* mean difference plot, diffeence 1015-2019, mean of differences. Differences in differences



* Then 


* Panel models with both paramters



*





* Plot of most greatest changes in base versus plot of greatest change in time-adjusted


* Run panel analyses with more confidence, in for, example, panel studies during this period








